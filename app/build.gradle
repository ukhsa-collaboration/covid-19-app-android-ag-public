import org.gradle.api.tasks.testing.logging.TestExceptionFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir/ktlint.gradle"
apply plugin: 'com.google.protobuf'
apply plugin: "com.github.triplet.play"
apply plugin: 'com.kncept.junit.reporter'

if (jacocoEnabled.toBoolean()) {
    apply from: rootProject.file("jacoco.gradle")
}

repositories {
    maven {
        url = 'https://maven.pkg.github.com/nhsx/covid-19-app-configuration-public'
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("PACKAGES_ACCESS_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("PACKAGES_ACCESS_TOKEN")
        }
    }
}

def buildNumber = new File('app/buildNumber').text as Integer
def versionNameValue = new File('app/versionName').text.trim()
def releaseDate = new Date().format('MM/yyyy')

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "uk.nhs.covid19"
        minSdkVersion 23
        targetSdkVersion 29
        versionName versionNameValue
        versionCode buildNumber
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "VERSION_NAME_SHORT", "\"" + versionNameValue + "\""
        buildConfigField "String", "RELEASE_DATE", "\"" + releaseDate + "\""
        // testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    adbOptions {
        installOptions '-g', '-r'
    }

    buildTypes {
        debug {
            testCoverageEnabled jacocoEnabled.toBoolean()
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        scenarios {
            proto {
                srcDirs 'src/scenarios/java/uk/nhs/nhsx/covid19/android/app/fieldtests'
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        animationsDisabled = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging.exceptionFormat = TestExceptionFormat.FULL
            testLogging.showStandardStreams = true
        }
        // execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        production {
            keyAlias System.getenv('PRODUCTION_UPLOAD_SIGNING_KEY_ALIAS') ?: 'dummy-keystore'
            keyPassword System.getenv('PRODUCTION_UPLOAD_SIGNING_KEY_PASSWORD') ?: 'secret'
            storeFile file(System.getenv('PRODUCTION_UPLOAD_SIGNING_KEY_STORE') ?: 'dummy-keystore')
            storePassword System.getenv('PRODUCTION_UPLOAD_SIGNING_KEY_STORE_PASSWORD') ?: 'secret'
        }
        scenarios {
            keyAlias System.getenv('INTERNAL_UPLOAD_SIGNING_KEY_ALIAS') ?: 'dummy-keystore'
            keyPassword System.getenv('INTERNAL_UPLOAD_SIGNING_KEY_PASSWORD') ?: 'secret'
            storeFile file(System.getenv('INTERNAL_UPLOAD_SIGNING_KEY_STORE') ?: 'dummy-keystore')
            storePassword System.getenv('INTERNAL_UPLOAD_SIGNING_KEY_STORE_PASSWORD') ?: 'secret'
        }
    }

    flavorDimensions "version"
    productFlavors {
        def scenariosSigning = signingConfigs.scenarios
        def productionSigning = signingConfigs.production

        scenarios {
            dimension "version"
            applicationIdSuffix ".internal"
            versionNameSuffix "-internal (${buildNumber})"
            signingConfig scenariosSigning
        }
        production {
            dimension "version"
            applicationIdSuffix ".production"
            versionNameSuffix " (${buildNumber})"
            signingConfig productionSigning
        }
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/licenses/ASM'
    }
}

junitHtmlReport {
    testResultsDir = 'outputs/androidTest-results/connected/flavors/scenariosDebugAndroidTest'
}

play {
    serviceAccountCredentials = file(System.getenv('PLAY_SERVICES_PUBLISH') ?: 'non-existing.json')
    defaultToAppBundles = true
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.2'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

def timber_version = "4.7.1"
def lifecycle_version = "2.2.0"
def work_version = "2.5.0-beta02"
def coroutines_version = "1.3.7"
def retrofit_version = "2.9.0"
def moshi_version = "1.9.2"
def okhttp_version = "4.8.0"
def configuration_version = "1.0.24-SNAPSHOT"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation 'com.google.android.material:material:1.1.0'
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'androidx.security:security-crypto:1.0.0-rc03'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'com.tinder.statemachine:statemachine:0.2.0'

    implementation 'com.google.android.gms:play-services-base:17.3.0'
    implementation 'com.google.android.gms:play-services-tasks:17.1.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"

    // Configurations
    implementation "uk.nhs.covid19:configuration-production:$configuration_version"
    scenariosImplementation "uk.nhs.covid19:configuration-scenarios:$configuration_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    scenariosImplementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    scenariosImplementation 'com.github.mrmike:ok2curl:0.6.0'

    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.awaitility:awaitility-kotlin:3.1.6'

    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.27'

    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestUtil 'androidx.test:orchestrator:1.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.awaitility:awaitility-kotlin:3.1.6'
    androidTestImplementation 'org.assertj:assertj-core:3.16.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation 'com.squareup.radiography:radiography:2.0.0'

    scenariosImplementation 'com.google.protobuf:protobuf-java:3.11.4'
    scenariosImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    androidTestImplementation('com.schibsted.spain:barista:3.6.0') {
        exclude group: 'org.jetbrains.kotlin'
    }

    implementation 'com.google.android.play:core:1.8.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    api 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.11.2')
}

afterEvaluate {
    dependencies {
        productionDebugImplementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
        productionDebugImplementation 'com.github.mrmike:ok2curl:0.6.0'
    }
}

apply from: "$rootDir/screenshots.gradle"
