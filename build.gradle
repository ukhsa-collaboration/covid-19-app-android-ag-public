// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:0.8.6"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
        classpath "com.github.triplet.gradle:play-publisher:3.0.0"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.13.1")
}

ext {
    jacocoEnabled = System.properties.getProperty("jacocoEnabled") ?: "false"
}

subprojects {
    apply {
        plugin("io.gitlab.arturbosch.detekt")
    }

    tasks.detekt {
        jvmTarget = "1.8"
    }

    detekt {
        buildUponDefaultConfig = true
        config = files("$rootDir/config/detekt/detekt.yml")

        reports {
            xml.enabled = false
            html.enabled = true
            txt.enabled = false
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url = 'https://maven.pkg.github.com/nhsx/covid-19-app-configuration-public'
            credentials {
                username = findProperty("gpr.user") ?: System.getenv("REPOS_ACCESS_ACTOR")
                password = findProperty("gpr.key") ?: System.getenv("REPOS_ACCESS_TOKEN")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task("localBuild") {
    dependsOn(":app:ktlint")
    dependsOn(":app:testScenariosDebugUnitTest")
    dependsOn(":app:connectedScenariosDebugAndroidTest")
}

defaultTasks("localBuild")
